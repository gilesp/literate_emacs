# -*- eval: (add-hook 'after-save-hook (lambda nil (org-babel-tangle)) nil t); -*-
:PROPERTIES:
:ID:       C6F07644-D261-483C-8296-6EA8F133C481
:END:

#+TITLE: Emacs Literate Configuration
#+AUTHOR: Giles Paterson
#+PROPERTY: header-args :tangle yes

:PROPERTIES:
:VISIBILITY: children
:END:

* Table of Contexts :TOC_3_gh:
- [[#about-this-file][About this file]]
  - [[#tangle-on-save][Tangle on Save]]
  - [[#visibility-settings][Visibility Settings]]
  - [[#table-of-contents][Table of Contents]]
- [[#early-init][Early Init]]
  - [[#disable-the-tool-bar-and-menu-bar][Disable the tool-bar and menu-bar]]
- [[#general-emacs-settings][General Emacs Settings]]
  - [[#use-package][Use-Package]]
  - [[#diminish][Diminish]]
  - [[#todo-garbage-collection-again][TODO: Garbage Collection (again)]]
  - [[#personal-information][Personal Information]]
  - [[#fix-defaults][Fix defaults]]
  - [[#automatically-revert-buffers][Automatically Revert Buffers]]
  - [[#tabs][Tabs]]
  - [[#disable-use-of-the-customize-file][Disable use of the customize file]]
  - [[#disable-backup-files][Disable backup files]]
  - [[#utf-8-all-the-things][UTF-8 all the things!]]
  - [[#unbind-the-pesky-sleep-key][Unbind the pesky sleep key]]
  - [[#use-y-or-n-instead-of-yes-or-no][Use 'y' or 'n' instead of 'yes' or 'no']]
  - [[#fix-macos-path-nonsense][Fix MacOS Path nonsense]]
  - [[#a-function-to-set-the-framesize-for-streaming][A function to set the framesize for streaming]]
  - [[#enable-recentf-mode-recent-files][Enable recentf mode (recent-files)]]
  - [[#enable-overwriting-selected-text][Enable overwriting selected text]]
  - [[#disable-audible-bell-use-visual-instead][Disable audible bell, use visual instead]]
  - [[#show-trailing-white-space][Show trailing white space]]
  - [[#highlight-current-line][Highlight current line]]
  - [[#undo-tree][Undo Tree]]
  - [[#theme][Theme]]
    - [[#mode-line][Mode line]]
  - [[#fonts][Fonts]]
  - [[#which-key][Which key]]
  - [[#smart-move-to-beginning-of-line][Smart move to beginning of line]]
- [[#yasnippet---text-completion][YASnippet - text completion]]
- [[#org-mode][Org Mode]]
  - [[#toc-org][Toc-org]]
  - [[#org-capture][Org Capture]]
  - [[#org-protocol][Org Protocol]]
  - [[#org-ql][org-ql]]
  - [[#org-present][org-present]]
- [[#markdown][Markdown]]
- [[#completion-framework][Completion Framework]]
  - [[#ivy][Ivy]]
  - [[#counsel][Counsel]]
  - [[#ivy-rich][Ivy-Rich]]
  - [[#swiper][Swiper]]
- [[#git-stuff][Git stuff]]
- [[#projectile][Projectile]]
  - [[#counsel-projectile-org-capture][counsel-projectile-org-capture]]
- [[#codetext-completion-company-mode][Code/Text Completion (company-mode)]]
- [[#parentheses][Parentheses]]
- [[#line-numbers][Line numbers]]
- [[#development-languages][Development languages]]
  - [[#treemacs][Treemacs]]
  - [[#javascriptweb-development-lsp][Javascript/web development (lsp?)]]
  - [[#java][Java]]
    - [[#lsp-mode-deprecated][lsp-mode (Deprecated)]]
    - [[#eglot][eglot]]
    - [[#cucumber-tests][Cucumber tests]]
  - [[#rust][Rust]]
- [[#yaml][Yaml]]
- [[#restclient][Restclient]]
- [[#ulisp][uLisp]]
- [[#remote-access-with-tramp][Remote access with Tramp]]
- [[#structurizr-mode][Structurizr Mode]]
- [[#sql-mode-disabled][SQL Mode (Disabled)]]

* About this file
This configuration is inspired by [[https://github.com/vidjuheffex/dotemacs][vidjuheffex's config]] and [[https://github.com/gilbertw1/emacs-literate-starter][gilbertw1's config]]. I've also taken inspiration from https://blog.sumtypeofway.com/posts/emacs-config.html and https://assortedarray.com/posts/my-init-org-setup/.

This is a literate configuration, meaning that it's an org file with embedded code blocks that form the configuration for emacs. Having the code blocks in an org file allows me to document everything with the code it relates to.

** Tangle on Save
   Rather than using a stub init.el that calls =org-babel-load-file= on startup, I'm using a hook to tangle this file when I save. This means that I only have one configuration file, and hopefully startup will be quicker too.

   I'm using a file variable, added with =M-x add-file-local-variable-prop-line=, to set the eval hook in the first line of this file.

   #+begin_src emacs-lisp :tangle no
     eval: (add-hook 'after-save-hook (lambda nil (org-babel-tangle)) nil t);
   #+end_src

   Alternatively, the file can be manually tangled with =M-x org-babel-tangle= or =C-c C-v t=.

** Visibility Settings
 Next we have a property defined on the [[Configuration][Configuration]] heading that defines the visibility that tells org to show it's direct children on startup. This way a clean outline of all sub headings under Configuration is shown each time this file is opened in org-mode.

** Table of Contents
 Finally, there is a [[Table of Contents][Table of Contents]] heading that includes the tag: =:TOC_3_gh:=. This tells an org-mode package =toc-org= to generate a table of contents under this heading that has a max depth of 3 and is created using Github-style hrefs. This table of contents is updated everytime the file is saved and makes for a functional table of contents that works properly directly on github.

* Early Init
  Emacs 27 introduced the =early-init.el= file that is loaded very early in the startup process. It's intended for customizing how the package system is initialized, so let's do our package config there:

  #+begin_src emacs-lisp :tangle early-init.el
    ;;; early-init.el -*- lexical-binding: t; eval: (view-mode -1); -*-
    (require 'package)
    (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
    (add-to-list 'package-archives '("nongnu" . "https://elpa.nongnu.org/nongnu/") t)
    (add-to-list 'package-archives '("gnu" . "https://elpa.gnu.org/packages/") t)
  #+end_src

  Taking inspiration from [[https://github.com/hlissner/doom-emacs/blob/develop/docs/faq.org#how-does-doom-start-up-so-quickly][doom-emacs]], I'm going to bump the garbage collection threshold up during startup to help speed things up. Then we reset it after startup.
   #+begin_src emacs-lisp :tangle early-init.el
     (defvar gp/gc-cons-threshold 16777216) ;; 16mb
     (setq gc-cons-threshold most-positive-fixnum
           gc-cons-percentage 0.6)
     (add-hook 'emacs-startup-hook
               (lambda ()
                 (setq gc-cons-threshold gp/gc-cons-threshold
                       gc-cons-percentage 0.1)))

   #+end_src

** Disable the tool-bar and menu-bar
 #+begin_src emacs-lisp :tangle early-init.el
   (tool-bar-mode -1)
   (menu-bar-mode -1)
 #+end_src

* General Emacs Settings
:PROPERTIES:
:ID:       473B06A5-3265-44C2-A20F-977D6586B83A
:END:
  Make elisp in this file have proper scoping. [[https://www.emacswiki.org/emacs/DynamicBindingVsLexicalBinding][This Emacswiki article explains it well.]]

  I also enable =view-mode=, to make the tangled file read-only, as a reminder that it is auto-generated and shouldn't be manually edited.

#+begin_src emacs-lisp :comments no
;;; init.el -*- lexical-binding: t ; eval: (view-mode -1); -*-
#+end_src

** Use-Package
   I like to use [[https://github.com/jwiegley/use-package][use-package]] to keep things tidy and speedy on startup, so this makes sure it's available. I set it to defer loading packages unless told otherwise, this speeds up initialisation.

 #+begin_src emacs-lisp
   (unless (package-installed-p 'use-package)
     (package-refresh-contents)
     (package-install 'use-package))
   (setq use-package-always-ensure t
         use-package-ensure-all t
         use-package-always-defer t)
   (require 'use-package)
   (require 'use-package-ensure)
 #+end_src

 I like to make sure all my packages are up to date, so I use auto-package-update.
 #+begin_src emacs-lisp
   (use-package auto-package-update
     :custom
     (auto-package-update-delete-old-versions t)
     :init
     (auto-package-update-maybe))
 #+end_src
** Diminish
Use diminish so that use-package can hide modes from the mode line when we ask it to.
#+begin_src emacs-lisp
(use-package diminish)
#+end_src

** TODO: Garbage Collection (again)
   Increase the garbage collection thresholds when using the minibuffer, and return to normal afterwards.

 #+begin_src emacs-lisp
   ;; ;; max memory available for gc when opening minibuffer
   ;; (defun gp/defer-garbage-collection-h ()
   ;;   (setq gc-cons-threshold most-positive-fixnum))

   ;; (defun gp/restore-garbage-collection-h ()
   ;;   ;; Defer it so that commands launched immediately after will enjoy the
   ;;   ;; benefits.
   ;;   (run-at-time
   ;;    1 nil (lambda () (setq gc-cons-threshold gp/gc-cons-threshold))))

   ;; (add-hook 'minibuffer-setup-hook #'gp/defer-garbage-collection-h)
   ;; (add-hook 'minibuffer-exit-hook #'gp/restore-garbage-collection-h)
 #+end_src
** Personal Information
    This is me
 #+begin_src emacs-lisp
 (setq user-full-name "Giles Paterson"
       user-mail-address "giles@vurt.uk")
 #+end_src

** Fix defaults
 Reset some emacs defaults that date from prehistoric times

 #+begin_src emacs-lisp
   (setq inhibit-startup-screen t)      ;; No need for a startup screen
   (setq initial-scratch-message nil)   ;; or to tell me what the scratch buffer is
   (setq inhibit-startup-echo-area-message t)
   (setq inhibit-startup-message t)
   (setq sentence-end-double-space nil) ;; Who the hell does this in 2021?
   (setq mark-even-if-inactive nil)     ;; Fix undo in command affecting the mark.
   (setq kill-whole-line t)             ;; Let C-k delete the whole line.
 #+end_src

** Automatically Revert Buffers
   If a file changes on disk, I want the buffer to update. Emacs will prompt if I have unsaved changes, so this is safe to do.
   #+begin_src emacs-lisp
     (global-auto-revert-mode 1)
   #+end_src
** Tabs
    Tabs, spaces. It's a whole thing. Fundamentally, I'd love for tabs to work like tabs, and allow users to set their own tab spacing, but that's not the world we live in anymore. So, I need to use spaces.
    #+begin_src emacs-lisp
      (setq-default indent-tabs-mode nil)
      (setq tab-width 4)
    #+end_src
** Disable use of the customize file
    I find this annoying, as it's all too easy to override something by accident with the customize functionality, then your emacs init doesn't work. You can't get rid of it completely, but you can point it at another file, to avoid cluttering up init.el

#+begin_src emacs-lisp
  (setq custom-file "~/.emacs.d/custom.el")
  (when (file-exists-p custom-file)
    (load custom-file))
  (setq custom-safe-themes t)
#+end_src

** Disable backup files
    I've never found emacs backup files to be particularly useful
#+begin_src emacs-lisp
  (setq make-backup-files nil)
  (setq auto-save-default nil)
  (setq create-lockfiles nil)
#+end_src

** UTF-8 all the things!
    utf-8 should be the default for everything.

#+begin_src emacs-lisp
  (set-charset-priority 'unicode)
  (setq locale-coding-system 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (set-selection-coding-system 'utf-8)
  (prefer-coding-system 'utf-8)
  (setq default-process-coding-system '(utf-8-unix . utf-8-unix))
#+end_src

** Unbind the pesky sleep key
#+begin_src emacs-lisp
  (global-unset-key [(control z)])
  (global-unset-key [(control x)(control z)])
#+end_src

** Use 'y' or 'n' instead of 'yes' or 'no'
#+begin_src emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+end_src

** Fix MacOS Path nonsense
   on OS X, where an Emacs instance launched as a GUI app inherits a default minimal set of environment variables that
   are probably not the ones you see in a terminal window. Similarly, if you start Emacs as a daemon from systemd or
   launchd, it will run with a default set of environment variables.

This library solves this problem by copying important environment variables from the user's shell: it works by asking your shell to print out the variables of interest, then copying them into the Emacs environment.

#+begin_src emacs-lisp
  (defconst *is-a-mac* (eq system-type 'darwin))
  (use-package exec-path-from-shell
    :ensure t)
  ;;Later on, after loading exec-path-from-shell package

  (if *is-a-mac*
     (add-hook 'after-init-hook 'exec-path-from-shell-initialize))
#+end_src

** A function to set the framesize for streaming
 Sometimes I want to show emacs via OBS Studio, which I have set to 1280x720p. A frame size of 109x31 fits perfectly for me. Here's a function I can call to switch to toggle between that frame size and my normal one, when I need it.
 #+begin_src emacs-lisp
   (defvar gp/frame-normal-width 110)
   (defvar gp/frame-normal-height 58)
   (defvar gp/frame-streaming-width-720 109)
   (defvar gp/frame-streaming-height-720 31)
   (defvar gp/frame-streaming-width-1080 188)
   (defvar gp/frame-streaming-height-1080 48)
   (defvar gp/is-streaming-size nil)

   (defun gp/frame-size-streaming-720 ()
     "Sets the frame size so it's suitable for streaming via OBS at 720p"
     (interactive)
     (set-frame-width (selected-frame) gp/frame-streaming-width-720)
     (set-frame-height (selected-frame) gp/frame-streaming-height-720)
     (setq gp/is-streaming-size t))

   (defun gp/frame-size-streaming-1080 ()
     "Sets the frame size so it's suitable for streaming via OBS at 1080p"
     (interactive)
     (set-frame-width (selected-frame) gp/frame-streaming-width-1080)
     (set-frame-height (selected-frame) gp/frame-streaming-height-1080)
     (setq gp/is-streaming-size t))

   (defun gp/frame-size-normal ()
     "Sets the frame size to my default values"
     (interactive)
     (set-frame-width (selected-frame) gp/frame-normal-width)
     (set-frame-height (selected-frame) gp/frame-normal-height)
     (setq gp/is-streaming-size nil))

   (defun gp/streaming-toggle ()
     "Toggles between streaming and normal frame sizes"
     (interactive)
     (if (eq gp/is-streaming-size nil)
       (gp/frame-size-streaming-720)
     ; else
     (gp/frame-size-normal)))

   (gp/frame-size-normal)
 #+end_src

** Enable recentf mode ([[https://www.emacswiki.org/emacs/RecentFiles][recent-files]])
Recentf is useful to re-open a file you worked on recently, but it's good to set some limits on it. Also, ignore any package files from elpa, as they make it less than useful.
#+begin_src emacs-lisp
  (require 'recentf)
  (recentf-mode 1)
  (setq recentf-max-menu-items 50)
  (setq recentf-max-saved-items 250)
  ;; (global-set-key "\C-x\ \C-r" 'recentf-open-files)
  (add-to-list 'recentf-exclude "\\elpa")
#+end_src

I've replaced the recentf-open-files keybinding with counsel-recentf instead.

** Enable overwriting selected text
#+begin_src emacs-lisp
  (delete-selection-mode t)
#+end_src

** Disable audible bell, use visual instead
#+begin_src emacs-lisp
  (setq ring-bell-function 'ignore)
  (setq visible-bell t)
#+end_src

** Show trailing white space
   Show white space at the ends of line, to avoid embarassment when comitting something. Then delete them with M-x delete-trailing-whitespace
  #+begin_src emacs-lisp
    (setq-default show-trailing-whitespace t)
  #+end_src

   actually, just delete them automatically when we save.
  #+begin_src emacs-lisp
    (add-hook 'before-save-hook #'delete-trailing-whitespace)
  #+end_src

  We don't want whitespace to be highlighted in all modes, notably the minibuffer, term and compilation buffers. So let's disable it for those modes
  #+begin_src emacs-lisp
    (dolist (hook '(special-mode-hook
                    term-mode-hook
                    comint-mode-hook
                    compilation-mode-hook
                    minibuffer-setup-hook))
      (add-hook hook
                (lambda () (setq show-trailing-whitespace nil))))
  #+end_src

** Highlight current line
This is useful in many situations, so enable it for programming and
text editing based modes.
#+begin_src emacs-lisp
  (require 'hl-line)
  (add-hook 'prog-mode-hook #'hl-line-mode)
  (add-hook 'text-mode-hook #'hl-line-mode)
#+end_src

** Undo Tree
Emacs has a powerful, but confusing, undo model. Undo tree makes it easier to use and lets you get back pretty much anything you've done.
#+begin_src emacs-lisp
(use-package undo-tree
  :diminish
  :bind (("C-c _" . undo-tree-visualize))
  :config
  (global-undo-tree-mode +1)
  (unbind-key "M-_" undo-tree-map))
#+end_src

** Theme
I like a dark editor, and base16-brewer is what I've got used previously. But now I'm trying modus-vivendi. Previously I used vscode-dark-plus but that doesn't work in emacs 30.
#+begin_src emacs-lisp
  (load-theme 'modus-vivendi t)
  ;; (use-package vscode-dark-plus-theme
  ;;   :ensure t
  ;;   :config
  ;;   (load-theme 'vscode-dark-plus t))
  ;; ;; (add-to-list 'custom-theme-load-path (concat user-emacs-directory "themes"))
  ;; (load-theme 'vscode-dark-plus t)
  ;; ;; Remove the border around the TODO word on org-mode files
  ;; (setq vscode-dark-plus-box-org-todo nil)

  ;; ;; Do not set different heights for some org faces
  ;; (setq vscode-dark-plus-scale-org-faces nil)
#+end_src

*** Mode line
    I'm using doom-modeline as it looks nice and is pretty customisable. +It depends on =all-the-icons= so you need to run =M-x all-the-icons-install-fonts= to make sure the fonts are installed.+

    Now it seems to depend on ~nerd-icons~ so you need to run ~M-x nerd-icons-install-fonts~ instead.
#+begin_src emacs-lisp
  (use-package doom-modeline
    :ensure t
    :custom
    (column-number-mode t)
    (doom-modeline-project-detection 'project)
    :init (doom-modeline-mode 1))
#+end_src

** Fonts
A matter of personal opinion, but a modern mono-spaced font makes emacs look a lot better.

I'm setting Roboto Light for the variable pitch font and Fira Code for fixed-pitch (i.e. monospace)

#+begin_src emacs-lisp
  ;; (defvar gp/fixed-width-font "Cascadia Code 14")
  (defvar gp/fixed-width-font "Fira Code")
  (defvar gp/fixed-width-weight 'medium)
  (defvar gp/fixed-width-height 140) ;; 1/10ths of pt (i.e. 14pt)
  (defvar gp/variable-width-font "Roboto")

  (set-face-attribute 'default nil :font gp/fixed-width-font :weight gp/fixed-width-weight :height gp/fixed-width-height)
  (set-face-attribute 'fixed-pitch nil :font gp/fixed-width-font :weight gp/fixed-width-weight :height gp/fixed-width-height)
  (set-face-attribute 'variable-pitch nil :font gp/variable-width-font :weight 'light :height 170)

  (dolist (face '(default fixed-pitch))
    (set-face-attribute `,face nil :font gp/fixed-width-font))

  ;;(add-to-list 'default-frame-alist '(font . "Roboto Mono Light 14"))
  ;;(set-fontset-font "fontset-default"  '(#x2600 . #x26ff) "Fira Code 16")
  (setq-default line-spacing 0)
  (setq x-underline-at-descent-line t)
#+end_src

** Which key
   [[https://github.com/justbur/emacs-which-key][Which-key]] enables discoverability of available commands.

   If you ever want to just explore the available keybindings, then try =M-x which-key-show-top-level= or =M-x which-key-show-major-mode=

#+begin_src emacs-lisp
  (use-package which-key
    :diminish
    :demand t
    :config
    (which-key-mode 1)
    (which-key-setup-side-window-bottom))
#+end_src

** Smart move to beginning of line
This function was originally taken from the [[https://emacsredux.com/blog/2013/05/22/smarter-navigation-to-the-beginning-of-a-line/][Emacs Redux]] blog. It means that pressing C-a will move the cursor to the start of the text on a line. Pressing it again will jump to the physical start of the line.

#+begin_src emacs-lisp
  (defun gp/smarter-move-beginning-of-line (arg)
  "Move point back to indentation of beginning of line.

Move point to the first non-whitespace character on this line.
If point is already there, move to the beginning of the line.
Effectively toggle between the first non-whitespace character and
the beginning of the line.

If ARG is not nil or 1, move forward ARG - 1 lines first.  If
point reaches the beginning or end of the buffer, stop there."
  (interactive "^p")
  (setq arg (or arg 1))

  ;; Move lines first
  (when (/= arg 1)
    (let ((line-move-visual nil))
      (forward-line (1- arg))))

  (let ((orig-point (point)))
    (back-to-indentation)
    (when (= orig-point (point))
      (move-beginning-of-line 1))))

;; remap C-a to `smarter-move-beginning-of-line'
(global-set-key [remap move-beginning-of-line]
                'gp/smarter-move-beginning-of-line)
#+end_src

* YASnippet - text completion

#+begin_src emacs-lisp
(use-package yasnippet
       :ensure t
       :init
       (yas-global-mode 1)
       :config
       (add-to-list 'yas-snippet-dirs (locate-user-emacs-file "snippets")))
#+end_src

* Org Mode
:PROPERTIES:
:ID:       66E848BA-D50A-427B-8660-1FCAD5A8FF8B
:END:

#+begin_src emacs-lisp
  (use-package org
    ;; :defer t
    :hook ((org-mode . visual-line-mode)
           (org-mode . variable-pitch-mode)
           (org-babel-after-execute . org-display-inline-images))
    :mode (("\\.org$" . org-mode))
    :init
    (defun gp/list-note-files ()
      "Get list of org files in my notes directory"
      (directory-files-recursively "~/Documents/syncthing/org/notes/" "\\`[^.].*\\.org\\'"))

    (defun gp/org-backlinks ()
      "Search for backlinks to current entry."
      (interactive)
        (let ((link (condition-case nil
                        (org-id-store-link)
                      (error "Unable to create a link to here"))))
          (org-occur-in-agenda-files (regexp-quote link))))

    ;; function to provide id: link compeltion based on my org files.
    (defun gp/org-id-complete-link (&optional arg)
      "Create an id: ink using completion."
      (concat "id:" (org-id-get-with-outline-path-completion org-refile-targets)))

    :custom

    (org-startup-with-inline-images t)
    (org-src-tab-acts-natively t)
    (org-src-fontify-natively t) ;; native src block fontification
    (org-src-window-setup 'current-window) ;; edit src blocks in place, rather than a new window
    (org-hide-emphasis-markers t) ;;actually emphasise text (e.g. show as italic instead of /italic/)
    (org-confirm-babel-evaluate nil)
    (org-indent-indentation-per-level 2)
    (org-adapt-indentation nil)
    ;; Make note files searchable without just adding them to agenda files, and slowing everything down.
    (org-agenda-text-search-extra-files (gp/list-note-files))
    (org-refile-targets '((nil :maxlevel . 9)
                          (org-agenda-files :maxlevel . 9)
                          (gp/list-note-files :maxlevel . 2)))

    (org-outline-path-complete-in-steps nil)         ; Refile in a single go
    (org-refile-use-outline-path t)                  ; Show full paths for refiling
    ;; use uuid for links
    (org-id-link-to-org-use-id t)
    (org-id-extra-files 'org-agenda-text-search-extra-files)

    ;; automatically indent headings and paragraphs
    (org-startup-indented t)

    :config
    ;; load more languages for org-babel
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (shell      . t)
       (sql        . t)))


    ;; This config sets my completion function to be called when inserting id: links
    (org-link-set-parameters "id"
                             :complete 'gp/org-id-complete-link)

    ;; In order for variable-pitch-mode to work properly, I need to set fixed pitch fonts for various org faces:
    (custom-theme-set-faces
     'user
     '(org-block ((t (:inherit fixed-pitch))))
     '(org-checkbox ((t (:inherit fixed-pitch))))
     '(org-code ((t (:inherit (shadow fixed-pitch)))))
     '(org-document-info-keyword ((t (:inherit (shadow fixed-pitch)))))
     '(org-formula ((t (:inherit (fixed-pitch)))))
     '(org-meta-line ((t (:inherit (font-lock-comment-face fixed-pitch)))))
     '(org-property-value ((t (:inherit fixed-pitch))) t)
     '(org-special-keyword ((t (:inherit (font-lock-comment-face fixed-pitch)))))
     '(org-table ((t (:inherit fixed-pitch))))
     '(org-tag ((t (:inherit (shadow fixed-pitch) :weight bold :height 0.8))))
     '(org-verbatim ((t (:inherit (shadow fixed-pitch)))))
     '(nxml-text ((t (:inherit (fixed-pitch))))))
    ;; (org-id-update-id-locations) ;; This line breaks org mode in emacs 30!
    )
#+end_src

It looks like I was using org-contrib to get org babel shell support, but I don't know if I still need that. org-contrib pulls in a lot of outdated stuff, so I'm disabling this for now.
#+begin_src emacs-lisp :tangle no
  ;; what do I get from org-contrib?
  (use-package org-contrib
    :after org)

    ;; ;; (require 'ob-dot) ;; have to do this for some reason, otherwise babel can't handle dot.
    ;; would be good to be able to do it via use-package

  (use-package ob-shell
    :ensure org-contrib
    :commands
    org-babel-execute:sh
    org-babel-expand-body:sh
    org-babel-execute:bash
    org-babel-expand-body:bash
    :custom
    (org-babel-do-load-languages
     'org-babel-load-languages
     (append org-babel-load-languages
             '(
               (sh . t)
               (bash . t)
               )))
    )
#+end_src


Let's make org-mode have dyanmic wrapping, and center it in the frame so that it works a bit more like a typical word processor.

For this, I make use of [[https://github.com/joostkremers/visual-fill-column][visual-fill-column]] , then text behaves as if you'd used M-q but without actually adding line breaks to the source text.

#+begin_src emacs-lisp
  (use-package visual-fill-column
    :diminish
    :hook (org-mode . visual-fill-column-mode)
    :custom
    (visual-fill-column-width 120)
    (visual-fill-column-center-text t))
#+end_src

Previously I used  [[https://github.com/rnkn/olivetti][olivetti mode]] instead, but this should be a lighter weight approach.

#+begin_src emacs-lisp :tangle no
  (use-package olivetti
    :diminish
    :init
    (setq-default fill-column 120)
    :config
    (olivetti-set-width 100) ;; olivetti mode seems to go wider then 100, so I set the fill column to 120 to visually match.
    (setq olivetti-style "fancy")
    :hook (org-mode . olivetti-mode))
#+end_src

To make org mode look a bit nicer, I like to use the org-bullets package to replace headline markers with Unicode bullets.
#+begin_src emacs-lisp
  (use-package org-bullets
    :diminish
    :hook (org-mode . org-bullets-mode))
#+end_src


** Toc-org
 Install the =toc-org= package after org mode is loaded. This enables automatic generation of up to
 date tables of contents.

 #+begin_src emacs-lisp
   (use-package toc-org
     :diminish
     :ensure t
     :after org
     :hook (org-mode . toc-org-mode))

 #+end_src
** Org Capture
:PROPERTIES:
:ID:       0B1D3C79-1E20-4F1C-907C-C86327A1C325
:END:
I'm trying to use org mode for managing a simple(ish) todo list. I want to be able to capture tasks quickly, then sort & action them appropriately.

I'll start with a single todo file and see how i get on with that.

I've also created a template for notes - it uses a function to create a new note file and then captures to that.
   #+begin_src emacs-lisp
     (use-package org-capture
       :ensure nil
       :after org-agenda
       :defer 1
       :bind (("C-c c" . org-capture))
       :config
       ;; set task priority range from A to C with default B
       (setq org-highest-priority ?A)
       (setq org-lowest-priority ?C)
       (setq org-default-priority ?B)

       ;; define todo states
       (setq org-todo-keywords
             '((sequence "TODO(t)" "WORK(w)" "DONE(d)")))

       ;; After a successful capture, update the org agenda extra files so that searching includes the newly captured file
       (defun gp/update-agenda-files ()
         (if org-note-abort
             ()
           (setq org-agenda-text-search-extra-files (gp/list-note-files))))

       (add-hook 'org-capture-after-finalize-hook 'gp/update-agenda-files)

       (defun gp/capture-note-file (path)
         "Create an org file in path"
         (let ((name (read-string "Name: ")))
           (expand-file-name (format "%s.org"
                                     name)
                             path)))

       (setq org-capture-templates
             '(
               ("t"    ; key
                "Todo" ; description
                entry  ; type
                (file+headline "~/Documents/syncthing/org/todo.org" "Tasks") ; target
                "* TODO [#B] %?" ;template
                )
               ("n"    ; key
                "Note" ; description
                entry  ; type
                (file (lambda () (gp/capture-note-file "~/Documents/syncthing/org/notes"))) ; target
                "* %? %(org-set-tags-command) \n%U" ; template
                )))
       )
   #+end_src

   I start by defining C-c c as the keybinding to trigger capture. This is set globally so I can capture a note from anywhere in emacs.

#+begin_src emacs-lisp
  (use-package org-agenda
    :ensure nil
    :after org
    :bind (("C-c a" . org-agenda))
    :custom
    ;; open agenda in current window
    (org-agenda-window-setup (quote current-window))
    ;; file to save todo items
    (org-agenda-files (quote ("~/Documents/syncthing/org/todo.org")))
    (add-to-list 'org-agenda-text-search-extra-files '(org-agenda-files :maxlevel . 2))
    )
#+end_src
** Org Protocol
   Org capture works well within emacs but if you want to make a note based on something in another application [[https://orgmode.org/manual/Protocols.html#Protocols][org-protocol]] can help.

   Firstly, we need to have the emacs server running, so emacsclient can talk to it.
   #+begin_src emacs-lisp
     (require 'server)
     (unless (server-running-p)
       (server-start))
   #+end_src

   Then we require org-protocol
   #+begin_src emacs-lisp
     (require 'org-protocol)
   #+end_src

   And that should be that. The host operating system needs to have a url handler registered for the org-protocol: url. For Mac OS, I've followed [[https://orgmode.org/manual/Protocols.html#Protocols][this approach using an apple script]].

   The "Web clip" capture template defined above can be used with this javascript bookmarklet to make a note using a selection from a web site.

   #+begin_src javascript :tangle no
     javascript:location.href='org-protocol://capture?template=w'+
      '&url='+encodeURIComponent(window.location.href)+
      '&title='+encodeURIComponent(document.title)+
      '&body='+encodeURIComponent(window.getSelection());
   #+end_src
** org-ql
I'm going to try using org-ql and dynamic blocks to provide backlinks in my org notes. This is the approach I describe in my note taking notes (searching_notes.org)

I can insert a dynamic block with C-c C-x x and select "backlinks" from the list. This will add the block and update it, so any links to the current org section will be listed.


#+begin_src emacs-lisp

  (use-package org-ql
    :after org
    :init
    (defun org-dblock-write:zorg-backlinks (&rest params)
      (let ((elements (org-ql-query
                       :from org-agenda-text-search-extra-files
                       :where `(link :target ,(org-id-store-link)))))
        (insert "Backlinks:\n")
        (dolist (element elements)
          (insert "- " (org-make-link-string (concat "id:" (org-element-property :ID element))
                                             (org-element-property :raw-value element))
                  "\n"))
        (delete-char -1)))

    ;; Enable inserting the dynamic block with C-x C-x x
    (defun org-insert-dblock:backlinks ()
      "Insert a dynamic block backlinks at point."
      (interactive)
      (atomic-change-group
        (org-create-dblock (list :name "zorg-backlinks")))
      (org-update-dblock))

    (org-dynamic-block-define "backlinks" #'org-insert-dblock:backlinks)
    ;; update dynamic blocks when saving
    ;; to ensure backlinks are always up to date.
    (add-hook 'before-save-hook #'org-update-all-dblocks)
    )
#+end_src
** org-present

For giving presentations with org mode. This is all taken from the System Crafters guide: https://systemcrafters.net/emacs-tips/presentations-with-org-present/

#+begin_src emacs-lisp
  (use-package org-present
    :init
    (defun gp/org-present-prepare-slide (buffer-name heading)
      ;; Show only top-level headlines
      (org-overview)

      ;; Unfold the current entry
      (org-show-entry)

      ;; Show only direct sub headings of the slide but don't expand them
      (org-show-children))

    (defun gp/org-present-start ()
      ;; Tweak font sizes
      (setq-local face-remapping-alist '((default (:height 1.5) variable-pitch)
                                         (header-line (:height 4.0) variable-pitch)
                                         (org-document-title (:height 1.75) org-document-title)
                                         (org-code (:height 1.55) org-code)
                                         (org-verbatim (:height 1.55) org-verbatim)
                                         (org-block (:height 1.25) org-block)
                                         (org-block-begin-line (:height 0.7) org-block)))

      ;; Set a blank header line string to create blank space at the top
      (setq header-line-format " ")

      ;; disable highlighting the current line
      (hl-line-mode -1)

      ;; hide the cursor
      ;;(org-present-hide-cursor)
      (setq-local cursor-type nil)

      ;; read only mode
      (org-present-read-only))

    (defun gp/org-present-end ()
      ;; Reset font customizations
      (setq-local face-remapping-alist '((default variable-pitch default)))

      ;; Clear the header line string so that it isn't displayed
      (setq header-line-format nil)

      ;; enable highlighting the current line
      (hl-line-mode)

      ;; show the cursor again
      ;;(org-present-show-cursor)
      (setq-local cursor-type 'box)

      ;; read write mode
      (org-present-read-write))

    :hook ((org-present-mode . gp/org-present-start)
           (org-present-mode-quit . gp/org-present-end)
           (org-present-after-navigate-functions . gp/org-present-prepare-slide))
    )
#+end_src


* Markdown
Sometimes I need to edit markdown, so here's how to configure [[https://github.com/jrblevin/markdown-mode][markdown-mode]].
For README.md files, use github flavoured markdown, otherwise use normal markdown mode.

#+begin_src emacs-lisp
  (use-package markdown-mode
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'" . markdown-mode)
           ("\\.markdown\\'" . markdown-mode)))
#+end_src

* Completion Framework

** Ivy
   I'm going to give Ivy a go (along with Swiper/Counsel) to see if I like it, instead of Helm.

 #+begin_src emacs-lisp
   (use-package ivy
     :diminish ivy-mode
     :custom
     (ivy-height 30)
     (ivy-use-virtual-buffers t)
     (ivy-use-selectable-prompt t)
     (ivy-count-format "(%d/%d) ")
     :config
     (ivy-mode 1))
 #+end_src

  I'll start with a minimal config - just setting the options recommended in the getting started section of the documentation.

** Counsel
   Similarly, for Counsel, I'll just enable counsel-mode to default to using counsel.
#+begin_src emacs-lisp
  (use-package counsel
    :diminish counsel-mode
    :bind (("C-x C-r" . counsel-recentf))
    :config
    (counsel-mode 1))

  (use-package counsel-projectile)
#+end_src

** Ivy-Rich
   Ivy-rich enables prettifying the ivy output
#+begin_src emacs-lisp
  (use-package ivy-rich
    :diminish ivy-rich-mode
    :hook ((ivy-mode counsel-mode) . ivy-rich-mode)
    :custom
    (ivy-virtual-abbreviate 'abbreviate)
    (ivy-rich-path-style 'abbrev)
    :config
    (setcdr (assq t ivy-format-functions-alist) #'ivy-format-function-line))
#+end_src

** Swiper
   And finally, swiper for searching. I bind it to C-s so that I use it instead of i-search.

   I'm now binding swiper-thing-at-point to C-s since I'm usually searching for th thing I'm looking at.
#+begin_src emacs-lisp
  (use-package swiper
    :commands (swiper swiper-all)
    :bind ("C-s" . 'swiper-thing-at-point))
#+end_src

* Git stuff
Magit is a great interface to git (although the [[https://magit.vc/manual/magit/][documentation]] is quite dense).
#+begin_src emacs-lisp
  (use-package magit
    :bind (("C-x g" . magit-status)
           ("C-x M-g" . magit-dispatch-popup)))
#+end_src
Apart from the keybindings, I don't need to make any config changes.

I like to have a visual git status in the gutter/fringe, for that I use [[https://github.com/emacsorphanage/git-gutter][git-gutter.el]]
#+begin_src emacs-lisp
  (use-package git-gutter
    :diminish git-gutter-mode
    :init
    (custom-set-variables
     '(git-gutter:update-interval 2))
    :config
    (global-git-gutter-mode +1))
#+end_src
Dimish the mode so that it doesn't clutter up our mode line/status bar.

The update-interval config is to enable live updating (every 2 seconds of idle time).

I enable it globally because I use git for many different files, not just code.

* Projectile
[[https://github.com/bbatsov/projectile][Projectile]] is handy for interacting with projects, and it can integrate with Helm or Ivy nicely.
#+begin_src emacs-lisp
  (use-package projectile
    :demand
    :bind (:map projectile-mode-map
              ("C-c p" . projectile-command-map))
    :init
    (setq projectile-completion-system 'ivy)
    (setq projectile-enable-caching t)
    :config
    (add-to-list 'projectile-globally-ignored-files "node-modules")
    (projectile-mode))
#+end_src

And since I'm currently using Ivy & Counsel, I'll include the [[https://github.com/ericdanan/counsel-projectile][counsel-projectile]] integration too.

#+begin_src emacs-lisp
  (use-package counsel-projectile
    :diminish
    :demand
    :config
    (counsel-projectile-mode))
#+end_src

** TODO [[https://github.com/ericdanan/counsel-projectile#setting-counsel-projectile-org-capture-templates][counsel-projectile-org-capture]]

* Code/Text Completion (company-mode)
Got to have those sweet code-completion popups, courtesy of [[https://company-mode.github.io/][company-mode]].
#+begin_src emacs-lisp
    (use-package company
      :diminish
      :bind (("C-." . #'company-complete))
      :custom
      (company-dabbrev-downcase nil "Don't downcase returned candidates.")
      (company-show-numbers t "Numbers are helpful.")
      (company-tooltip-limit 20 "The more the merrier.")
      (company-tooltip-idle-delay 0.4 "Faster!")
      (company-async-timeout 20 "Some requests can take a long time. That's fine.")
      :config
      ;; Use the numbers 0-9 to select company completion candidates
      (let ((map company-active-map))
        (mapc (lambda (x) (define-key map (format "%d" x)
                            `(lambda () (interactive) (company-complete-number ,x))))
              (number-sequence 0 9)))
      :init
      (setq company-tooltip-minimum-width 10)
      (global-company-mode))
#+end_src
I've had issues with company popups being ragged when I'm using a proportional font. So I'm using [[https://github.com/tumashu/company-posframe/][company-posframe]] to enable child frames instead.
#+begin_src emacs-lisp
  (use-package company-posframe
    :init
    (company-posframe-mode 1))
#+end_src

* Parentheses
I find it invaluable to have parentheses matching enabled.
#+begin_src emacs-lisp
  (show-paren-mode t)
#+end_src
And I like the visual delimitation of colour. Rainbow-delimiters adds that.
#+begin_src emacs-lisp
  (use-package rainbow-delimiters
    :diminish
    :hook (prog-mode . rainbow-delimiters-mode))
#+end_src

* Line numbers
I want line numbers on all code editing buffers. Since they should all
derive from prog-mode, I'll set line numbers there and hope for the
best. The alternative is to enable global line numbers then turn it
off in other modes, but that seems messier to me.
#+begin_src emacs-lisp
(add-hook 'prog-mode-hook 'display-line-numbers-mode)
#+end_src

* Development languages
  I'm going to try lsp-mode again, for languages it supports.

  Emacs 27 with lsp-mode is super fast and really good to use.

  Here's the core lsp-configuration:
#+begin_src emacs-lisp :tangle no
  (use-package lsp-mode
    :hook (
           ;; bind lsp to the development modes I'm interested in.
           (web-mode . lsp-deferred)
           (yaml-mode . lsp-deferred)
           (java-mode . lsp-deferred)
           (lsp-mode . lsp-enable-which-key-integration))
    :init
    (setq lsp-keymap-prefix "C-l")
    (setq lsp-enable-completion-at-point t)
    (setq lsp-enable-indentation t)
    (setq lsp-enable-on-type-formatting t)
    (setq gc-cons-threshold 100000000)
    (setq read-process-output-max (* 1024 1024)) ;; 1mb
    :commands lsp lsp-deferred)

  (use-package lsp-ui)
#+end_src
I've changed the default prefix from "s-l" to "C-l".

According to the [[https://emacs-lsp.github.io/lsp-mode/page/performance/][lsp-mode documentation]], a few things should be tweaked for maximum performance.

The default setting is too low for lsp-mode's needs due to the fact that client/server communication generates a lot of memory/garbage. I've taken the easy approach of just setting this to 100mb in the lsp-mode init section above.
#+begin_src emacs-lisp :tangle no
(setq gc-cons-threshold 100000000)
#+end_src

Similarly the the amount of data which Emacs reads from the process needs increasing. The emacs default (4k) is too low considering that some of the language server responses are in 800k - 3M range.

#+begin_src emacs-lisp :tangle no
(setq read-process-output-max (* 1024 1024)) ;; 1mb
#+end_src

Turn on ivy integration for lsp:
#+begin_src emacs-lisp :tangle no
    (use-package lsp-ivy
      :after (ivy lsp-mode)
      :commands lsp-ivy-workspace-symbol)
#+end_src

** Treemacs

   Sometimes I want an IDE style tree view, so lets enable treemacs and lsp-treemacs
#+begin_src emacs-lisp :tangle no
  (use-package lsp-treemacs
    :config
    (lsp-treemacs-sync-mode 1)
    :commands lsp-treemacs-errors-list
    :after (treemacs lsp-mode))
#+end_src

Treemacs provides a very configurable tree-view. I'm going to see how I get on with it, so I've just grabbed a basic config from the [[https://github.com/Alexander-Miller/treemacs][treemacs documentation]].

C-x t t should toggle the treeview, and I'll see how I get on with that.

#+begin_src emacs-lisp
  (use-package treemacs
    :ensure t
    :defer t
    :init
    (with-eval-after-load 'winum
      (define-key winum-keymap (kbd "M-0") #'treemacs-select-window))
    :config
    (progn
      ;; The default width and height of the icons is 22 pixels. If you are
      ;; using a Hi-DPI display, uncomment this to double the icon size.
      ;;(treemacs-resize-icons 44)

      (treemacs-tag-follow-mode t)
      (treemacs-filewatch-mode t)
      (treemacs-fringe-indicator-mode 'always)
      (treemacs-git-mode 'simple))
    :bind
    (:map global-map
          ("M-0"       . treemacs-select-window)
          ("C-x t 1"   . treemacs-delete-other-windows)
          ("C-x t t"   . treemacs)
          ("C-x t B"   . treemacs-bookmark)
          ("C-x t C-t" . treemacs-find-file)
          ("C-x t M-t" . treemacs-find-tag)))
#+end_src

And since we're also using projectile, let's enable treemacs integration
#+begin_src emacs-lisp
  (use-package treemacs-projectile
    :after (treemacs projectile)
    :ensure t)
#+end_src

Make things pretty with all-the-icons
#+begin_src emacs-lisp
  (use-package treemacs-all-the-icons
    :after (treemacs))
#+end_src

** Javascript/web development (lsp?)
   There are several ways to configure javascript & typescript support. I'm going with web-mode since it can handle template-based development (react, vue etc.) along with raw js & ts files.

#+begin_src emacs-lisp
  (use-package web-mode
    :ensure t
    :mode (("\\.js\\'" . web-mode)
           ("\\.jsx\\'" . web-mode)
           ("\\.ts\\'" . web-mode)
           ("\\.tsx\\'" . web-mode)
           ("\\.html\\'" . web-mode)
           ("\\.vue\\'" . web-mode)
           ("\\.json\\'" . web-mode))
    :commands web-mode
    :config
    (setq company-tooltip-align-annotations t)
    (setq web-mode-markup-indent-offset 2)
    (setq web-mode-css-indent-offset 2)
    (setq web-mode-code-indent-offset 2)
    (setq web-mode-enable-part-face t)
    (setq web-mode-content-types-alist
          '(("jsx" . "\\.js[x]?\\'")))
    )
#+end_src

You will need to install the [[https://github.com/sourcegraph/javascript-typescript-langserver][javascript-typescript-langserver]] for lsp to work with javascript.
#+begin_src sh :tangle no
npm i -g javascript-typescript-langserver
#+end_src

Alternatively, this could be installed as a docker container, and then you could avoid installing npm/node on your local machine. I'll have to give that another go sometime.

** Java

*** lsp-mode (Deprecated)
   Setup lsp-java
   #+begin_src emacs-lisp :tangle no
     (use-package lsp-java
      :hook (java-mode . lsp)
      :bind (:map java-mode-map
                  ([tab] . company-indent-or-complete-common)))
   #+end_src

   Also configure dap-mode for debugging.
   #+begin_src emacs-lisp :tangle no
     (use-package dap-mode
       :after lsp-mode

       :config (dap-auto-configure-mode)
       (add-hook 'dap-stopped-hook
                 (lambda (arg) (call-interactively #'dap-hydra))))

     (use-package dap-java
       :ensure nil)
   #+end_src

*** eglot

Setup eglot-java
#+begin_src emacs-lisp
  (use-package eglot-java
    :hook (java-mode . eglot-java-mode)
    :bind (:map eglot-java-mode-map
                ("C-c <tab>" . company-complete)
                ("C-c l n" . eglot-java-file-new)
                ("C-c l x" . eglot-java-run-main)
                ("C-c l t" . eglot-java-run-test)
                ("C-c l N" . eglot-java-project-new)
                ("C-c l T" . eglot-java-project-build-task)
                ("C-C l R" . eglot-java-project-build-refresh))
    )
  #+end_src
    When doing maven builds etc, it's nice to have the compilation buffer autoscroll:
  #+begin_src emacs-lisp
    (setq compilation-scroll-output t)
   #+end_src

*** Cucumber tests
#+begin_src emacs-lisp
  (use-package feature-mode
    :mode (("\\.feature\\'" . feature-mode)))
#+end_src
** Rust
Setup rustic and lsp for Rust programming

For this to work, you need rust-analyzer installed. The easiest method is to use rustup:
#+begin_src sh :tangle no
  rustup component add rust-analyzer
#+end_src

#+begin_src emacs-lisp :tangle no
  (use-package rustic
  :ensure
  :bind (:map rustic-mode-map
              ("M-j" . lsp-ui-imenu)
              ("M-?" . lsp-find-references)
              ("C-c C-c l" . flycheck-list-errors)
              ("C-c C-c a" . lsp-execute-code-action)
              ("C-c C-c r" . lsp-rename)
              ("C-c C-c q" . lsp-workspace-restart)
              ("C-c C-c Q" . lsp-workspace-shutdown)
              ("C-c C-c s" . lsp-rust-analyzer-status)
              ([tab] . company-indent-or-complete-common))
  :config
  ;; uncomment for less flashiness
  ;; (setq lsp-eldoc-hook nil)
  ;; (setq lsp-enable-symbol-highlighting nil)
  ;; (setq lsp-signature-auto-activate nil)
  (setq lsp-rust-analyzer-server-display-inlay-hints t)
  (setq lsp-rust-analyzer-display-lifetime-elision-hints-enable "skip_trivial")
  (setq lsp-rust-analyzer-display-chaining-hints t)
  (setq lsp-rust-analyzer-display-lifetime-elision-hints-use-parameter-names nil)
  (setq lsp-rust-analyzer-display-closure-return-type-hints t)
  (setq lsp-rust-analyzer-display-parameter-hints nil)
  (setq lsp-rust-analyzer-display-reborrow-hints nil)
  ;; comment to disable rustfmt on save
  (setq rustic-format-on-save t))

#+end_src

* Yaml
#+begin_src emacs-lisp
  (use-package yaml-mode
    :ensure t
    :mode ("\\.ya?ml\\'" . yaml-mode))
#+end_src

* Restclient
[[https://github.com/pashky/restclient.el][A tool for interacting with webservices.]]
[[https://erick.navarro.io/blog/testing-an-api-with-emacs-and-restclient/][This]] is a good intro to using restclient.
Bind it to .http files.
#+begin_src emacs-lisp
  (use-package restclient
    :ensure t
    :mode ("\\.http\\'" . restclient-mode))
#+end_src

We can enable completion for it via company mode
#+begin_src emacs-lisp
  (use-package company-restclient
    :ensure t
    :after (company restclient)
    :hook ((restclient-mode-hook . (lambda() (setq company-backend (company-restclient))))))
#+end_src

And, of course, we can integrate it with org mode:
#+begin_src emacs-lisp
  (use-package ob-restclient
     :ensure t
     :defer t
     :after org restclient)
#+end_src

* uLisp



    I've installed [[http://www.ulisp.com/show?3KN3][uLisp]] on a Raspberry Pi Pico, and it should be possible to use inferior lisp mode to interact with it.


 #+begin_src emacs-lisp
   (defvar port "/dev/ttyACM1" "rasoberry-pi-pico")
   (defvar bauds 9600 "Bps")
   (defun sb-open()
     (let ((serial-buffer (serial-term port bauds)))
       (with-current-buffer
           (rename-buffer "*inferior-lisp*")
         (term-line-mode)
         (setq inferior-lisp-buffer serial-buffer))))
  #+end_src

* Remote access with Tramp
  Tramp is an emacs feature that lets you edit a file on a remote machine via ssh and other methods. I only really want ssh, so I'll set that as the default

#+begin_src emacs-lisp
  (setq tramp-default-method "ssh")
#+end_src

* Structurizr Mode
This is my custom structurizr mode (available on github at https://github.com/gilesp/structurizr-mode).

#+begin_src emacs-lisp
  `((user :default ,user-login-name))
  (use-package structurizr-mode
    :ensure nil
    :load-path "~/projects/structurizr-mode"
    :mode ("\\.dsl\\'" . structurizr-mode))
#+end_src

* SQL Mode (Disabled)

Use ob-sql-mode to enable sql mode in org files

  #+begin_src emacs-lisp :tangle no
    (use-package ob-sql-mode
      :ensure t)
#+end_src

And set up some sql-mode connections
#+begin_src emacs-lisp :tangle no
  (setq sql-connection-alist
        '((productinfo-test (sql-product 'oracle)
                            (sql-server "oracle-productinfodb-test.service.test.fruugo")
                            (sql-user "productinfo")
                            (sql-password "productinfo")
                            (sql-database "orcl"))))
        '((primary-db (sql-product 'postgres)
                      (sql-database (concat "postgresql://"
                                            "username"  ;; replace with your username
                                            ":" (read-passwd "Enter password: ")
                                            "@host"      ;; replace with your host
                                            ":port"      ;; replace with your port
                                            "/database"  ;; replace with your database
                                            )))
          (secondary-db (sql-product 'postgres)
                        (sql-database (concat "postgresql://"
                                              "username:"
                                              (read-passwd "Enter password: ")
                                              "@host"
                                              ":port"
                                              "/database")))))
#+end_src
